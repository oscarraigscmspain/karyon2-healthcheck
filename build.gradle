plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

scmVersion {
    ignoreUncommittedChanges = false
}

sourceCompatibility = 1.8

project.version = scmVersion.version

repositories {
    mavenLocal()
    maven {
        credentials {
            username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: ''
            password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: ''
        }

        url "http://artifactory.scmspain.com/artifactory/repo"
    }
    mavenCentral()
}


dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
    testCompile 'com.sun.jersey:jersey-core:1.19'
    testCompile 'com.netflix.karyon:karyon2-archaius:2.7.1'
    testCompile 'com.tngtech.java:junit-dataprovider:1.10.0'

    compile 'javax.ws.rs:jsr311-api:1.1.1'
    compile 'com.netflix.karyon:karyon2-core:2.7.1'
    compile 'com.netflix.karyon:karyon2-archaius:2.7.1'
    compile 'com.netflix.karyon:karyon2-governator:2.7.1'
    compile 'org.reflections:reflections:0.9.9'
    compile 'com.scmspain.karyon:karyon-rest-router:1.2.+'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Task publishMavenJavaPublicationToMavenRepository is added in runtime so doFirst doesn't work propertly and must be done this way
tasks.whenTaskAdded { t ->
    if( t.name == "publishMavenJavaPublicationToMavenRepository"){
        if (!project.hasProperty('artifactory_scm_user') || !project.hasProperty('artifactory_scm_password')) {
            throw new GradleException("Properties 'artifactory_scm_user' and 'artifactory_scm_password' are mandatory to do a publish, please add this information in your ~/.gradle/gradle.properties")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.scmspain'
            version project.version

            from components.java

            artifact sourcesJar
            artifact javadocJar

        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: ''
                password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: ''
            }

            url "http://artifactory.scmspain.com/artifactory/libs-release-ms"
        }
    }
}
